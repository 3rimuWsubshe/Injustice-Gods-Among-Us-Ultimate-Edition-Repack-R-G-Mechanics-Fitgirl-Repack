## state.strict = true
## state.modname = 'nelua'
## state.nohashcodenames = true

!!cinclude '<stdlib.h>'
!!cinclude '<string.h>'

local function memcpy(dest: pointer, src: pointer, n: csize): pointer !cimport 'memcpy' end
local function memmove(dest: pointer, src: pointer, n: csize): pointer !cimport 'memmove' end
local function memset(s: pointer, c: cint, n: csize): pointer !cimport 'memset' end

local function malloc(size: csize): pointer !cimport 'malloc' end
local function calloc(nmemb: csize, size: csize): pointer !cimport 'calloc' end
local function realloc(ptr: pointer, size: csize): pointer !cimport 'realloc' end
local function free(ptr: pointer): void !cimport 'free' end

global memory = @record{}

local span = @record {
  data: byte[0]*,
  size: usize
}
global memory.span = span

function memory.alloc(size: usize): span
  assert(size > 0)
  local data = @byte[0]*(malloc(size))
  assert(data)
  return span { data, size }
end

function memory.alloc0(size: usize): span
  assert(size > 0)
  local data = @byte[0]*(calloc(1, size))
  assert(data)
  return span { data, size }
end

function memory.realloc(s: span*, size: usize)
  assert(size > 0)
  s.data = @byte[0]*(realloc(s.data, size))
  assert(s.data)
  s.size = size
end

function memory.realloc0(s: span*, size: usize)
  assert(size > 0)
  s.data = @byte[0]*(realloc(s.data, size))
  assert(s.data)
  if size > s.size then
    memset(&s.data[s.size], 0, size - s.size)
  end
  s.size = size
end

function memory.dealloc(s: span*)
  if s.size == 0 then
    return
  end
  free(s.data)
  s.data = nilptr
  s.size = 0
end

function memory.copy(dest: span, src: span)
  assert(dest.size >= src.size)
  memcpy(dest.data, src.data, src.size)
end

function memory.move(dest: span, src: span)
  assert(dest.size >= src.size)
  memcpy(dest.data, src.data, src.size)
end

function memory.set(dest: span, x: byte)
  memset(dest.data, x, dest.size)
end
