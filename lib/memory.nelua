-- Memory utilities

--------------------------------------------------------------------------------
-- C imports
local function memcpy(dest: pointer, src: pointer, n: csize): pointer <cimport'memcpy',cinclude'<string.h>',nodecl> end
local function memmove(dest: pointer, src: pointer, n: csize): pointer <cimport'memmove',cinclude'<string.h>',nodecl> end
local function memset(s: pointer, c: cint, n: csize): pointer <cimport'memset',cinclude'<string.h>',nodecl> end
local function memcmp(s1: pointer, s2: pointer, n: csize): cint <cimport'memcmp',cinclude'<string.h>',nodecl> end
local function memchr(s: pointer, c: cint, n: csize): pointer <cimport'memchr',cinclude'<string.h>',nodecl> end

--------------------------------------------------------------------------------
-- pointer functions

global memory = @record{}

function memory.copy(dest: pointer, src: pointer, size: usize) <inline>
  check(dest and src, 'memory.copy: invalid pointer')
  memcpy(dest, src, size)
end

function memory.move(dest: pointer, src: pointer, size: usize) <inline>
  check(dest and src, 'memory.move: invalid pointer')
  memmove(dest, src, size)
end

function memory.set(dest: pointer, x: byte, size: usize) <inline>
  check(dest, 'memory.set: invalid pointer')
  memset(dest, x, size)
end

function memory.zero(dest: pointer, size: usize) <inline>
  check(dest, 'memory.zero: invalid pointer')
  memset(dest, 0, size)
end

function memory.compare(a: pointer, b: pointer, size: usize): int32 <inline>
  check(a and b, 'memory.move: invalid pointer')
  return memcmp(a, b, size)
end

function memory.equals(a: pointer, b: pointer, size: usize): boolean <inline>
  check(a and b, 'memory.equals: invalid pointer')
  return memcmp(a, b, size) == 0
end

function memory.scan(p: pointer, x: byte, size: usize): pointer <inline>
  check(p, 'memory.scan: invalid pointer')
  return memchr(p, x, size)
end

function memory.find(heystack: pointer, heystacksize: usize,
                     needle: pointer, needlesize: usize): pointer
  if unlikely(needlesize == 0 or heystack == needle) then return heystack
  elseif unlikely(needlesize > heystacksize) then return nilptr
  else
    check(heystack and needle, 'memory.find: invalid pointer')
    if needlesize == 1 then
      return memchr(heystack, $(@byte*)(needle), heystacksize)
    end
    local heystackbegin: usize = (@usize)(heystack)
    for i:usize=heystackbegin,heystackbegin+(heystacksize-needlesize) do
      local p: pointer = (@pointer)(i)
      if memcmp(p, needle, needlesize) == 0 then
        return p
      end
    end
    return nilptr
  end
end

--------------------------------------------------------------------------------
-- span functions

require 'span'

local is_span = #[concept(function(x) return x.type.is_span end)]#

##[[
local function check_type_match(a, b)
  staticassert(a.type == b.type,
    "argument '%s' type '%s' differs from argument '%s' type '%s",
    a.name, b.name, a.type, b.type)
end
local function check_span_subtype_match(s, x)
  staticassert(s.type.subtype == x.type,
    "in argument '%s': type '%s' differs from argument '%s' type '%s",
    s.name, x.name, s.type.subtype, x.type)
end
]]

function memory.spancopy(dest: is_span, src: is_span) <inline>
  local T: type = #[dest.type.subtype]#
  ## check_type_match(dest, src)
  check(dest.size == src.size, 'memory.spancopy: span sizes must be equal')
  if likely(src.size > 0) then
    memcpy(dest.data, src.data, src.size * #T)
  end
end

function memory.spanmove(dest: is_span, src: is_span) <inline>
  local T: type = #[dest.type.subtype]#
  ## check_type_match(dest, src)
  check(dest.size == src.size, 'memory.spanmove: span sizes must be equal')
  if likely(src.size > 0) then
    memmove(dest.data, src.data, src.size * #T)
  end
end

function memory.spanset(dest: is_span, x: auto)
  ## check_span_subtype_match(dest, x)
  local T: type = #[x.type]#
  for i:usize=0,<dest.size do
    memcpy(&dest[i], &x, #T)
  end
end

function memory.spanzero(dest: is_span) <inline>
  local T: type = #[dest.type.subtype]#
  if likely(dest.size > 0) then
    memset(dest.data, 0, dest.size * #T)
  end
end

function memory.spancompare(a: is_span, b: is_span): int32
  ## check_type_match(a, b)
  local size: usize <noinit>
  if a.size < b.size then
    size = a.size
  else
    size = b.size
  end
  local ret: int32
  if likely(size > 0) then
    ret = memcmp(a.data, b.data, size)
  end
  if ret == 0 and a.size ~= b.size then
    if a.size < b.size then
      ret = -1
    else
      ret = 1
    end
  end
  return ret
end

function memory.spanequals(a: is_span, b: is_span): boolean <inline>
  ## check_type_match(a, b)
  local ret: boolean = a.size == b.size
  if ret and likely(a.size > 0) then
    ret = memcmp(a.data, b.data, a.size) == 0
  end
  return ret
end

function memory.spanfind(s: is_span, x: auto): isize
  ## check_span_subtype_match(s, x)
  local T: type = #[x.type]#
  for i:usize=0,<s.size do
    if memcmp(&s[i], &x, #T) == 0 then
      return (@isize)(i)
    end
  end
  return -1
end

function memory.spancontains(s: is_span, x: auto): boolean
  ## check_span_subtype_match(s, x)
  local T: type = #[x.type]#
  for i:usize=0,<s.size do
    if memcmp(&s[i], &x, #T) == 0 then
      return true
    end
  end
  return false
end

--------------------------------------------------------------------------------
-- moving values

local is_value_pointer = #[concept(function(x)
  return x.type.is_pointer and x.type.subtype.size > 0
end)]#

-- Returns a memory copy of the dereference of pointer `a` leaving its contents zero filled.
-- This is mainly used as an optimization to skip value __copy.
function memory.moveval(a: is_value_pointer) <inline>
  local T: type = #[a.type.subtype]#
  local ret: T <noinit>
  memcpy(&ret, a, #T)
  memset(a, 0, #T)
  return ret
end

-- Swaps memory of the dereference of pointer `a` and `b`.
-- This is mainly used as an optimization to skip value __copy or __destroy.
function memory.swapval(a: is_value_pointer, b: is_value_pointer) <inline>
  ## staticassert(a.type == b.type, 'cannot swap different types')
  local N: usize <comptime> = #[a.type.subtype.size]#
  local tmp: byte[N] <noinit>
  memcpy(&tmp[0], a, N)
  memcpy(a, b, N)
  memcpy(b, &tmp[0], N)
end
