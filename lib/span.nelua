-- Span type
--
-- This type is used as view to elements of a contiguous memory block.
-- Contiguous containers like vector, sequence and array can be viewed as a span.
-- Its elements starts at index 0 and go up to length-1 (like C arrays).
--
-- This type is specially useful for making functions with arguments that
-- are agnostic to the input container type.

require 'iterators'

## local make_span = generalize(function(T)
  ##[[
  static_assert(traits.is_type(T), "invalid type '%s", T)
  static_assert(not T.is_comptime, "spans cannot be of type '%s'", T)
  ]]
  local T = @#[T]#
  local spanT <nickname #[string.format('span(%s)', T)]#> = @record {
    data: *[0]T,
    size: usize
  }

  ##[[
  local spanT = spanT.value
  spanT.is_contiguous = true
  spanT.is_container = true
  spanT.is_span = true
  spanT.subtype = T
  ]]

  -- Returns reference to element at index `i`.
  -- If `i` is greater of equal to span size, then throws a runtime error on debug builds.
  function spanT:__atindex(i: usize): *T <inline,nosideeffect>
    check(i < self.size, 'index out of range')
    return &self.data[i]
  end

  -- Returns the number of elements in the span.
  function spanT:__len(): isize <inline,nosideeffect>
    return (@isize)(self.size)
  end

  -- Returns true if the span `data` ~= nilptr and `size` > 0.
  function spanT:valid(): boolean <inline,nosideeffect>
    return self.data ~= nilptr and self.size > 0
  end

  -- Initializes span from pointers to other arrays.
  function spanT.__convert(values: #[concept(function(x)
    if x.type.is_pointer and x.type.subtype and x.type.subtype:is_contiguous_of(T) then
      return true
    elseif x.type.is_string and (T.is_integral and T.bitsize == 8) then
      return true
    end
    return false, string.format("no viable conversion from '%s' to '%s'", x.type, spanT)
  end)]#): spanT <inline>
    local self: spanT
    ## if values.type.is_string then
      self.data = (@*[0]T)(values.data)
      self.size = values.size
    ## else
      if #values > 0 then
        self.data = (@*[0]T)(&values[#[values.type.subtype.is_sequence and 1 or 0]#])
        self.size = (@usize)(#$values)
      end
    ## end
    return self
  end

  ## return spanT
## end)

global span = #[make_span]#
