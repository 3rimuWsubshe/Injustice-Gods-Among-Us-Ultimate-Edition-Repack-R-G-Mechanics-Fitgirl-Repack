require 'span'
require 'memory'
--require 'allocators.generic'

require 'allocators.gc'
local allocator = @gc_allocator

-- C imports
local errno: cint <cimport,cinclude'<errno.h>',nodecl>
local function strlen(s: cstring): csize <cimport,cinclude'<string.h>',nodecl> end
local function toupper(c: cint): cint <cimport,cinclude'<ctype.h>',nodecl> end
local function tolower(c: cint): cint <cimport,cinclude'<ctype.h>',nodecl> end
local function strtoll(str: cstring, endptr: cstring*, base: cint): clonglong <cimport,cinclude'<stdlib.h>',nodecl> end
local function strtod(str: cstring, endptr: cstring*): float64 <cimport,cinclude'<stdlib.h>',nodecl> end

global string <codename 'nelua_string'> = @record{
  --refs: usize*,
  data: byte[0]*,
  size: usize
}

##[[
  local stringtype = context.rootscope.symbols.string.value
  stringtype.is_string = true
  stringtype.is_stringy = true
  stringtype.nick = 'string'
  stringtype.name = 'string'
  primtypes.stringview.metatype = types.MetaType()
  primtypes.stringview.metatype:inherit(stringtype.metatype)
]]

function string._create(size: usize): string <inline>
  local self: string
  check(size > 0, 'cannot create string with size 0')
  self.size = size
  self.data = (@byte[0]*)(allocator.alloc(size+1))
  self.data[size] = 0
  --self.refs = 1
  return self
end

function string:reset() <inline>
  $self = {}
end

function string.fromspan(s: span(byte)): string
  if unlikely(s.size == 0) then return (@string){} end
  local self = string._create(s.size)
  memory.copy(&self.data[0], s.data, s.size)
  return self
end

function string.fromcstring(s: cstring): string <inline>
  if unlikely(not s) then return (@string){} end
  local size: usize = strlen(s)
  if unlikely(size == 0) then return (@string){} end
  local self: string = string._create(size)
  memory.copy(&self.data[0], s, size)
  return self
end

--[[
function string:__copy(o: string*) <inline>
  self = o
  if likely(self) then
    self.refs = self.refs + 1
  end
end

function string:__destroy() <inline>
  if unlikely(self.size == 0) then return end
  self.refs = self.refs - 1
  if self.refs == 0 then
    allocator.dealloc(self)
    self = nilptr
  end
end
]]

local empty_cstring: cstring <const> = ''
function string.__tocstring(s: string): cstring <inline>
  if unlikely(s.size == 0) then return empty_cstring end
  return (@cstring)(&s.data[0])
end

function string.__tostringview(s: string): stringview <inline>
  if unlikely(s.size == 0) then return (@stringview){} end
  return (@stringview){data = (@cstring)(&s.data[0]), size = s.size}
end

function string.__convert(s: #[concept(function(s)
    return s.type.is_stringview or s.type.is_cstring
  end)]#): string <inline>
  ## if s.type.is_stringview then
    return (@string){size=s.size, data=s.data}
  ## elseif s.type.is_cstring then
    return string.fromcstring(s)
  ## end
end

global function tostring(x: auto): string
  ## if x.type.is_arithmetic then
    local size: cint
    ##[[
    local tyformat = require'nelua.cdefs'.types_printf_format[x.type.codename]
    staticassert(tyformat)
    cinclude '<stdio.h>'
    cinclude '<inttypes.h>'
    cemit('size = snprintf(NULL, 0, '..tyformat..', x);')
    ]]
    if unlikely(size == 0) then return (@string){} end
    local self: string = string._create(size)
    local dest: cstring = (@cstring)(&self.data[0])
    local n: csize = (@csize)(size+1)
    ## cemit('snprintf(dest, n, '..tyformat..', x);')
    return self
  ## elseif x.type.is_boolean then
    if x == true then
      return 'true'
    end
    return 'false'
  ## else
  ##   staticerror("tostring: cannot convert type '%s' to a string", x.type)
  ## end
end

function stringview.__tostring(s: stringview) <inline>
  if unlikely(s.size == 0) then return (@string){} end
  local ret: string = string._create(s.size)
  memory.copy(&ret.data[0], s.data, s.size)
  return ret
end

function string.__len(a: stringview): isize <inline>
  return (@isize)(a.size)
end

function string.__eq(a: stringview, b: stringview): boolean
  return a.size == b.size and (a.size == 0 or memory.equals(a.data, b.data, a.size))
end

function string.__lt(a: stringview, b: stringview): boolean
  if unlikely(a.size == 0 or b.size == 0) then
    return a.size < b.size
  else
    if a.size < b.size then
      return memory.compare(a.data, b.data, a.size) <= 0
    else
      return memory.compare(a.data, b.data, b.size) < 0
    end
  end
end

function string.__le(a: stringview, b: stringview): boolean
  if unlikely(a.size == 0 or b.size == 0) then
    return a.size <= b.size
  else
    if a.size <= b.size then
      return memory.compare(a.data, b.data, a.size) <= 0
    else
      return memory.compare(a.data, b.data, b.size) < 0
    end
  end
end

function string.__concat(a: stringview, b: stringview): string
  if a.size == 0 then
    return b
  elseif b.size == 0 then
    return a
  end
  local s: string = string._create(a.size + b.size)
  memory.copy(&s.data[0], a.data, a.size)
  memory.copy(&s.data[a.size], b.data, b.size)
  return s
end

function string.len(self: stringview): isize <inline>
  return (@isize)(self.size)
end

function string.byte(self: string, i: isize): byte
  if unlikely(self.size == 0) then return 0 end
  if unlikely(i < 0) then
    i = self.size + i + 1
  end
  --TODO: return nil instead on failure
  check(i >= 1 and i <= self.size, 'string.byte: index out of range')
  return self.data[i-1]
end

function string.sub(self: string, i: isize, j: isize): string
  if unlikely(self.size == 0) then return self end
  if unlikely(i < 0) then i = self.size + i + 1 end
  if unlikely(i <= 0) then i = 1 end
  if unlikely(j < 0) then j = self.size + j + 1 end
  if unlikely(j > self.size) then j = self.size end
  if unlikely(i == 1 and j == self.size) then return self end
  if unlikely(i > j) then return (@string){} end
  local size: usize = (@usize)(j - i + 1)
  if unlikely(size == 0) then return (@string){} end
  local s: string = string._create(size)
  memory.copy(&s.data[0], &self.data[i-1], size)
  return s
end

function string.rep(self: string, n: isize): string
  if unlikely(self.size == 0 or n <= 0) then return (@string){} end
  if unlikely(n == 1) then return self end
  local s: string = string._create(n * self.size)
  for i:isize=0,<n do
    memory.copy(&s.data[i*self.size], &self.data[0], self.size)
  end
  return s
end

function string.reverse(self: string): string
  if unlikely(self.size == 0) then return self end
  local s: string = string._create(self.size)
  for i:usize=0,<self.size do
    s.data[i] = self.data[self.size - i - 1]
  end
  return s
end

function string.upper(self: string): string
  if unlikely(self.size == 0) then return self end
  local s: string = string._create(self.size)
  for i:usize=0,<self.size do
    s.data[i] = (@byte)(toupper(self.data[i]))
  end
  return s
end

function string.lower(self: string): string
  if unlikely(self.size == 0) then return self end
  local s: string = string._create(self.size)
  for i:usize=0,<self.size do
    s.data[i] = (@byte)(tolower(self.data[i]))
  end
  return s
end

function string.char(c: byte)
  --TODO: handle more arguments
  local s: string = string._create(1)
  s.data[0] = c
  return s
end

function string.find(self: string,
                     pattern: stringview,
                     init: isize,
                     plain: boolean): (isize, isize)
  --TODO: handle patterns and returns them
  --TODO: return nil if not found instead of zeros
  check(plain, 'string.find: patterns not supported yet')
  if unlikely(self.size == 0) then
    if pattern.size == 0 then return 1, 0 end
    return 0, 0
  end
  if unlikely(init < 0) then init = self.size + init + 1 end
  if unlikely(init > self.size) then return 0, 0 end
  if unlikely(init <= 0) then init = 1 end
  local found = memory.find(&self.data[init-1], self.size-init+1, pattern.data, pattern.size)
  if found then
    local sbegin: isize = (@isize)((@usize)(found) - (@usize)(&self.data[0]))
    return sbegin + 1, sbegin + (@isize)(pattern.size)
  end
  return 0, 0
end

-- TODO: implement functions bellow
-- string.format(fmt, ...)
-- string:match(pattern, init)
-- string:gmatch(pattern)
-- string:gsub(pattern, repl, n)
-- string.pack(fmt, v1, v2, ..)
-- string.unpack(fmt, s, pos)
-- string.packsize(fmt)

local function str2int(a: stringview): integer <nosideeffect>
  local endptr: cstring
  errno = 0
  local i: clonglong = strtoll(a.data, &endptr, 10)
  check(endptr ~= a.data and errno == 0, 'failed to convert string to integer')
  return (@integer)(i)
end

local function str2num(a: stringview): number <nosideeffect>
  local endptr: cstring
  errno = 0
  local n: float64 = strtod(a.data, &endptr)
  check(endptr ~= a.data and errno == 0, 'failed to convert string to number')
  return n
end

global function tonumber(x: auto): number
  ## if x.type.is_arithmetic then
    return (@number)(x)
  ## elseif x.type.is_stringy then
    -- TODO: return nil in case of fail (like lua)
    return str2num(x)
  ## else
    ## staticerror("unable to convert value of type '%s' to a number", x.type)
  ## end
end

global function tointeger(x: auto): integer
  ## if x.type.is_arithmetic then
    return (@integer)(x)
  ## elseif x.type.is_stringy then
    -- TODO: return nil in case of fail (like lua)
    return str2int(x)
  ## else
    ## staticerror("unable to convert value of type '%s' to an integer", x.type)
  ## end
end

local is_arithmetic_convertible = #[concept(function(x)
  return x.type.is_string or x.type.is_stringview or x.type.is_string or x.type.is_arithmetic
end)]#

function string.__add(a: is_arithmetic_convertible, b: is_arithmetic_convertible): number
  return tonumber(a) + tonumber(b)
end

function string.__sub(a: is_arithmetic_convertible, b: is_arithmetic_convertible): number
  return tonumber(a) - tonumber(b)
end

function string.__mul(a: is_arithmetic_convertible, b: is_arithmetic_convertible): number
  return tonumber(a) * tonumber(b)
end

function string.__div(a: is_arithmetic_convertible, b: is_arithmetic_convertible): number
  return tonumber(a) / tonumber(b)
end

function string.__idiv(a: is_arithmetic_convertible, b: is_arithmetic_convertible): number
  return tonumber(a) // tonumber(b)
end

function string.__mod(a: is_arithmetic_convertible, b: is_arithmetic_convertible): number
  return tonumber(a) % tonumber(b)
end

function string.__pow(a: is_arithmetic_convertible, b: is_arithmetic_convertible): number
  return tonumber(a) ^ tonumber(b)
end

function string.__unm(a: is_arithmetic_convertible): number
  return -tonumber(a)
end

function string.__band(a: is_arithmetic_convertible, b: is_arithmetic_convertible): integer
  return tointeger(a) & tointeger(b)
end

function string.__bor(a: is_arithmetic_convertible, b: is_arithmetic_convertible): integer
  return tointeger(a) | tointeger(b)
end

function string.__bxor(a: is_arithmetic_convertible, b: is_arithmetic_convertible): integer
  return tointeger(a) ~ tointeger(b)
end

function string.__shl(a: is_arithmetic_convertible, b: is_arithmetic_convertible): integer
  return tointeger(a) << tointeger(b)
end

function string.__shr(a: is_arithmetic_convertible, b: is_arithmetic_convertible): integer
  return tointeger(a) >> tointeger(b)
end

function string.__bnot(a: is_arithmetic_convertible): integer
  return ~tointeger(a)
end
