require 'stringbuilder'
require 'string'
require 'resourcepool'
require 'allocators.general'

--------------------------------------------------------------------------------
-- C imports

local FILEptr = @pointer
local errno: cint <cimport,cinclude'<errno.h>',nodecl>
local stdin: FILEptr <cimport,cinclude'<stdio.h>',nodecl>
local stdout: FILEptr <cimport,cinclude'<stdio.h>',nodecl>
local stderr: FILEptr <cimport,cinclude'<stdio.h>',nodecl>
local SEEK_SET: cint <const,cimport,cinclude'<stdio.h>',nodecl>
local SEEK_END: cint <const,cimport,cinclude'<stdio.h>',nodecl>
local SEEK_CUR: cint <const,cimport,cinclude'<stdio.h>',nodecl>
local _IOFBF: cint <const,cimport,cinclude'<stdio.h>',nodecl>
local _IOLBF: cint <const,cimport,cinclude'<stdio.h>',nodecl>
local _IONBF: cint <const,cimport,cinclude'<stdio.h>',nodecl>
local EOF: cint <const,cimport,cinclude'<stdio.h>',nodecl>
local function strerror(errnum: cint): cstring <cimport,cinclude'<string.h>',nodecl> end
local function fopen(pathname: cstring, mode: cstring): FILEptr <cimport,cinclude'<stdio.h>',nodecl> end
local function fclose(fp: FILEptr): cint <cimport,cinclude'<stdio.h>',nodecl> end
local function fflush(fp: FILEptr): cint <cimport,cinclude'<stdio.h>',nodecl> end
local function fread(ptr: pointer, size: csize, n: csize, fp: FILEptr): csize <cimport,cinclude'<stdio.h>',nodecl> end
local function fwrite(ptr: pointer, size: csize, n: csize, fp: FILEptr): csize <cimport,cinclude'<stdio.h>',nodecl> end
local function fseek(fp: FILEptr, offset: clong, whence: cint): cint <cimport,cinclude'<stdio.h>',nodecl> end
local function ftell(fp: FILEptr): clong <cimport,cinclude'<stdio.h>',nodecl> end
local function ferror(fp: FILEptr): cint <cimport,cinclude'<stdio.h>',nodecl> end
local function setvbuf(fp: FILEptr, buf: cstring, modes: cint, n: csize): cint <cimport,cinclude'<stdio.h>',nodecl> end
local function clearerr(fp: FILEptr): void <cimport,cinclude'<stdio.h>',nodecl> end
local function getc(fp: FILEptr): cint <cimport,cinclude'<stdio.h>',nodecl> end
local function atexit(func: function()): cint <cimport,cinclude'<stdlib.h>',nodecl> end

--------------------------------------------------------------------------------
-- filestream

global filestream = @record{
  id: uint64
}
## filestream.value.is_filestream = true

local function fileresult(success: boolean): (boolean, stringview, integer)
  if not success then
    return false, strerror(errno), errno
  end
  return true, '', 0
end

local pool: resourcepool(FILEptr, GeneralAllocator)

-- free the pool on exit
local function free_pool() pool:destroy() end
atexit(free_pool)

function filestream._from_fp(fp: FILEptr): filestream
  return filestream{id = pool:allocinit(fp)}
end

function filestream:_get_fp(): FILEptr
  local p: FILEptr* = pool:get(self.id)
  if p then return $p end
  return nilptr
end

function filestream.open(filename: stringview, mode: #[optional_concept(stringview)]#) : (filestream, stringview, integer)
  ## if mode.type.is_nil then
  local mode: stringview = "r"
  ## end
  --TODO: check modes
  local fp: FILEptr = fopen(filename, mode)
  if not fp then
    return filestream{}, strerror(errno), errno
  end
  return filestream._from_fp(fp), '', 0
end

function filestream:flush(): (boolean, stringview, integer)
  local fp: FILEptr = self:_get_fp()
  if not fp then error('filestream.flush: attempt to use a closed file') end
  return fileresult(fflush(fp) == 0)
end

function filestream:close(): (boolean, stringview, integer)
  local fp: FILEptr = self:_get_fp()
  if not fp then error('filestream.close: attempt to use a closed file') end
  if fp == stdout or fp == stderr or fp == stdin then
    return false, 'cannot close standard file', -1
  end
  local res: cint = fclose(fp)
  pool:free(self.id)
  return fileresult(res == 0)
end

function filestream:seek(whence: #[optional_concept(stringview)]#,
                         offset: #[optional_concept(integer)]#): (integer, stringview, integer)
  local fp: FILEptr = self:_get_fp()
  if not fp then error('filestream.seek: attempt to use a closed file') end
  ## if whence.type.is_niltype then
    local len: integer = ftell(fp)
    return len, '', 0
  ## else
    ## if offset.type.is_niltype then
    local offset: integer = 0
    ## end
    local op: cint
    if whence == 'set' then op = SEEK_SET
    elseif whence == 'end' then op = SEEK_END
    elseif whence == 'cur' then op = SEEK_CUR
    else error("filestream.seek: invalid whence") end
    local len: integer, errmsg: stringview, status: integer
    if fseek(fp, (@clong)(offset), op) == 0 then
      len = ftell(fp)
    else
      len = -1
      local success: boolean
      success, errmsg, status = fileresult(false)
    end
    return len, errmsg, status
  ## end
end

function filestream:setvbuf(mode: stringview, size: #[optional_concept(integer)]#)
  ## if size.type.is_niltype then
  local size: csize = 1024
  ## end
  local fp: FILEptr = self:_get_fp()
  if not fp then error('filestream.setvbuf: attempt to use a closed file') end
  local op: cint
  if mode == 'no' then op = _IONBF
  elseif mode == 'full' then op = _IOFBF
  elseif mode == 'line' then op = _IOLBF
  else error('filestream.setvbuf: invalid mode') end
  return fileresult(setvbuf(fp, nilptr, op, size) == 0)
end

local READ_CHUNK_SIZE <comptime> = 1024

local function readline(sb: stringbuilder*, fp: FILEptr, chop: boolean): boolean
  -- TODO: lock file on POSIX?
  local NL: byte <comptime> = '\n'_byte
  local c: cint
  repeat
    local nr: uint32 = 0
    local buff: byte[0]* = sb:prepare(READ_CHUNK_SIZE) -- preallocate buffer space
    while nr < READ_CHUNK_SIZE do -- read up to buffer limit
      c = getc(fp)
      if c == EOF or c == NL then -- end of line
        break
      end
      buff[nr] = (@byte)(c)
      nr = nr + 1
    end
    sb:commit(nr)
  until c == EOF or c == NL -- until end of line
  if not chop and c == NL then -- want a newline and have one?
    sb:write_byte((@byte)(c)) -- add ending newline to result
  end
  -- return ok if read something (either a newline or something else)
  return (c == NL or sb.size > 0)
end

local function readchars(sb: stringbuilder*, fp: FILEptr, n: usize): boolean
  local p: pointer = sb:prepare(n)
  local nr: csize = fread(p, 1, n, fp)
  sb:commit(nr)
  return nr > 0
end

local function readall(sb: stringbuilder*, fp: FILEptr): boolean
  repeat -- read in chunks
    local p: pointer = sb:prepare(READ_CHUNK_SIZE)
    local nr: csize = fread(p, 1, READ_CHUNK_SIZE, fp)
    sb:commit(nr)
  until nr < READ_CHUNK_SIZE
  return true
end

function filestream:read(fmt: #[overload_concept{integer,stringview,niltype}]#): (string, stringview, integer)
  --TODO: multiple arguments
  local fp: FILEptr = self:_get_fp()
  if not fp then error('filestream.read: attempt to use a closed file') end
  local sb: stringbuilder
  local success: boolean
  clearerr(fp)
  ## if fmt.type.is_niltype then
    success = readline(&sb, fp, true)
  ## elseif fmt.type.is_arithmetic then
    success = readchars(&sb, fp, fmt)
  ## elseif fmt.type.is_stringview then
    local c: byte = fmt.data[0]
    if c == '*'_byte then -- skip optional '*' (for compatibility)
      c = fmt.data[1]
    end
    -- TODO: handle 'n' to read number
    if c == 'l'_byte then -- line
      success = readline(&sb, fp, true)
    elseif c == 'L'_byte then -- line with end-of-line
      success = readline(&sb, fp, false)
    elseif c == 'a'_byte then -- entire file
      success = readall(&sb, fp)
    else
      error('filestream.read: invalid format')
    end
  ## end
  local errmsg: stringview
  local status: integer = 0
  if ferror(fp) ~= 0 then
    success, errmsg, status = fileresult(false)
  end
  local s: string
  if success then
    s = sb:promotestring()
  else
    sb:destroy()
  end
  return s, errmsg, status
end

function filestream:write(s: stringview): (boolean, stringview, integer)
  --TODO: multiple arguments
  --TODO: return file itself
  local fp: FILEptr = self:_get_fp()
  if not fp then error('filestream.write: attempt to use a closed file') end
  local len: csize = s.size
  if fwrite(s.data, 1, len, fp) == len then
    return true, '', 0
  end
  return fileresult(false)
end

function filestream:isopen(): boolean
  return pool:has(self.id)
end

function filestream:__tostring(): string
  local fp: FILEptr = self:_get_fp()
  if not fp then
    return 'file (closed)'
  else
    return string.format('file (%p)', fp)
  end
end

-- TODO: filestream:lines
