require 'allocators.arena'
require 'allocators.stack'
require 'allocators.pool'
require 'allocators.heap'
require 'vector'

do -- Arena
  local allocator: ArenaAllocator(1024,8)
  local i: *integer = (@*integer)(allocator:alloc0(#integer))
  assert($i == 0)
  $i = 0xff
  assert($i == 0xff)
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 8)
  allocator:dealloc(i)
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 0)
  local ni: *integer = (@*integer)(allocator:alloc0(#integer))
  assert(ni == i and $ni == 0)
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 8)
  local a: *[0]integer = (@*[0]integer)(allocator:realloc0(i, 32 * #@integer, #@integer))
  assert((@*integer)(a) == i)
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 8*32)
  for i=0,<32 do a[i] = 0xff end
  a = (@*[0]integer)(allocator:realloc0(i, 8 * #@integer, 32 * #@integer))
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 8*8)
  for i=0,<8 do assert(a[i] == 0xff) end
  assert(allocator:realloc0(i, 0, 8 * #@integer) == nilptr)
  for i=0,<32 do assert(a[i] == 0xff) end
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 0)
  a = (@*[0]integer)(allocator:realloc0(nilptr, 1024, 0))
  assert(a ~= nilptr)
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 1024)
  assert(allocator:alloc0(1) == nilptr)
  allocator:deallocall()
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 0)
  for i=0,<1024 do
    assert(allocator.buffer[i] == 0)
  end
end

do -- Stack
  local allocator: StackAllocator(1024, 16)
  assert(allocator:alloc0(1024) == nilptr)
  local a = allocator:alloc0(3)
  assert(a ~= nilptr and (@usize)(a) % 16 == 0)
  local b = allocator:alloc0(7)
  assert(b ~= nilptr and (@usize)(b) % 16 == 0)
  local c = allocator:alloc0(33)
  assert(c ~= nilptr and (@usize)(c) % 16 == 0)
  local nc = allocator:realloc0(c, 133, 33)
  assert(nc == c)
  assert(allocator:realloc0(b, 99, 7) == nilptr)
  assert(allocator:realloc0(b, 1, 7) == b)
  local d = allocator:alloc0(1)
  allocator:dealloc(d)
  allocator:dealloc(c)
  allocator:dealloc(b)
  allocator:dealloc(a)
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 0)
  a = allocator:alloc0(3)
  b = allocator:alloc0(7)
  allocator:deallocall()
  assert(allocator.prev_offset == 0 and allocator.curr_offset == 0)
end

do -- Pool
  local Object = @record{
    x: integer,
    y: integer
  }
  local allocator: PoolAllocator(Object, 2)
  local o: *Object = (@*Object)(allocator:alloc0(#Object))
  assert(allocator.initialized == true)
  assert(o.x == 0)
  o.x = 0xff
  assert(o.x == 0xff)
  allocator:dealloc(o)
  -- must reuse the same free address
  local no: *Object = (@*Object)(allocator:alloc0(#Object))
  assert(no == o and no.x == 0)
  -- last avail memory
  allocator:alloc0(#Object)
  -- should be out of memory
  assert(allocator:alloc0(#Object) == nilptr)
  -- free all
  allocator:deallocall()
  -- realloc
  local i: *integer = (@*integer)(allocator:realloc0(nilptr, #integer, 0))
  local ri: *integer = (@*integer)(allocator:realloc0(i, 2*#integer, #integer))
  -- should use the same address
  assert(i == ri)
  -- should deallocate
  ri = (@*integer)(allocator:realloc(ri, 0, 2*#integer))
  assert(ri == nilptr)
end

do -- Heap
  local Allocator = @HeapAllocator(4096, true)
  local IntVector = @vector(integer, *Allocator)
  local allocator: Allocator
  local va = IntVector.make(&allocator)
  local vb = IntVector.make(&allocator)
  for i=1,100 do
    va:push(1)
    vb:push(1)
  end
  vb:destroy()
  va:destroy()
end

print 'allocators OK!'
